buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.24.14')
        classpath('pl.allegro.tech.build:axion-release-plugin:1.13.3')
    }
}

plugins {
    id "org.sonarqube" version "3.3"
    id "org.owasp.dependencycheck" version "6.2.2"
    id 'com.github.ben-manes.versions' version '0.39.0'
}

apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'idea'

jar {
    baseName = 'yti-spring-security'
}

scmVersion {
    tag {
        prefix = "v"
        versionSeparator = ""
    }
}

project.version = scmVersion.version

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

repositories {
    mavenCentral()
    jcenter()
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    springVersion = '5.5.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'fi.vm.yti'
            artifactId 'yti-spring-security'
            version project.version

            pom.withXml {
                asNode().appendNode('description', 'Common spring security configuration')
            }
            artifact jar
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

artifactory {
    publish {
        defaults {
            publications('maven')
            publishArtifacts = true
            publishPom = true
        }
    }
}

dependencies {
    compile "javax.servlet:javax.servlet-api:4.0.1"
    compile "com.fasterxml.jackson.core:jackson-databind:2.8.11.3"
    compile "org.springframework.security:spring-security-core:$springVersion"
    compile "org.springframework.security:spring-security-config:$springVersion"
    compile "org.springframework.security:spring-security-web:$springVersion"
    compile "org.apache.httpcomponents:httpclient:4.5.13"
    compile "com.google.code.findbugs:jsr305:3.0.2"
    compile "org.jetbrains:annotations:15.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "yti-spring-security"
        property "sonar.projectName", "yti-spring-security"
        property("sonar.dependencyCheck.reportPath", "$buildDir/reports/dependency-check-report.xml")
        property("sonar.dependencyCheck.htmlReportPath", "$buildDir/reports/dependency-check-report.html")
    }
}

dependencyCheck {
    format = 'ALL'
    autoUpdate = false
    analyzers {
        centralEnabled = false
    }
    data {
        directory = System.getenv('OWASP_DATA_DIR')
    }
}
